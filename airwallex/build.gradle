apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply from: "./ktlint.gradle"

def DEVICE_FINGERPRINT_ORG_ID = '"1snn5n9w"'
def DEVICE_FINGERPRINT_MERCHANT_ID = '"airwallex_cybs"'
def API_VERSION = '"2020-04-30"'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion

        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        consumerProguardFiles 'proguard-rules.pro'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}


android.buildTypes.each { type ->
    type.buildConfigField 'String', 'DEVICE_FINGERPRINT_ORG_ID', DEVICE_FINGERPRINT_ORG_ID
    type.buildConfigField 'String', 'DEVICE_FINGERPRINT_MERCHANT_ID', DEVICE_FINGERPRINT_MERCHANT_ID
    type.buildConfigField 'String', 'API_VERSION', API_VERSION
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'com.android.support:customtabs:28.0.0'
    implementation 'com.android.support:design:28.0.0'

    // cardinal
    api 'io.github.airwallex:cardinalmobilesdk:2.2.3-2'
    // fingerprinting
    api 'io.github.airwallex:TrustDefender:5.0-96'

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-parcelize-runtime:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-android-extensions-runtime:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"
}

ext {
    libraryVersion = "${android.defaultConfig.versionName}"
}

apply from: rootProject.file('publish.gradle')
apply from: rootProject.file('deploy.gradle')